################################################################################
#
# RevBayes Validation Test: Multispecies coalescent process
#
# Model: Multispecies coalescent with constant effective population size.
#        We test whether we can estimate the species tree, given fixed gene trees and Ne.
#
#
# authors: Bastien Boussau
#
################################################################################

#######################
# Reading in the Data #
#######################

### Read in an ultrametric tree

primate_tree <- readTrees( "data/primates.tree" )[1]


####################
# Useful variables #
####################

num_loci = 20

# Get some useful variables from the data. We need these later on.
n_species <- primate_tree.ntips()
taxa <- primate_tree.taxa()
n_branches <- 2 * n_species - 1 # number of branches in a rooted tree


moves = VectorMoves()
monitors = VectorMonitors()


######################
# Species Tree model #
######################

# Specify a prior on the diversification and turnover rate
diversification <- 0.1 #~ dnExp(1)
turnover <- 0.1 #~ dnExp(1)

# now transform the diversification and turnover rates into speciation and extinction rates
speciation := diversification + turnover
extinction := turnover

# specify a prior on the root age (our informed guess is about 75-80 mya)
root <- primate_tree.rootAge()

sampling_fraction <- 1.0


# construct a variable for the tree drawn from a birth death process
psi ~ dnBDP(lambda=speciation, mu=extinction, rootAge=abs(root), rho=sampling_fraction, taxa=taxa, condition="nTaxa" )
psi.setValue( primate_tree )


###################
# Gene-Tree model #
###################

# We assume a single effective population size for the entire species tree.
Ne ~ dnGamma(shape=1.0,rate=1.0)
moves.append(mvScale(Ne,1,true,1.0) )

for (i in 1:num_loci) {
   # The gene trees from the multispecies coalescent process
   # The taxon names in the gene trees are the same as in the species tree
   geneTree[i] ~ dnMultiSpeciesCoalescent(speciesTree=psi, Ne=Ne, taxa=taxa)
   geneTree[i].clamp(geneTree[i])
}

#############################
# Moves on the species tree #
#############################
moves.append( mvNarrow(psi, weight=5.0) )
moves.append( mvNNI(psi, weight=1.0) )
moves.append( mvFNPR(psi, weight=3.0) )
moves.append( mvSubtreeScale(psi, weight=3.0) )
moves.append( mvNodeTimeSlideUniform(psi, weight=15.0) )


#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model( psi )

mymcmc = mcmc(mymodel, monitors, moves)


validation = validationAnalysis( mymcmc, 100 )
validation.burnin(generations=1000,tuningInterval=100)
validation.run(generations=5000)
validation.summarize()


# you may want to quit RevBayes now
q()
