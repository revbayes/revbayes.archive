################################################################################
#
# RevBayes Validation Test: Jukes-Cantor with birth-death process
# 
# Model: Sequences evolve under the Jukes-Cantor model and the tree is drawn
#        from a constant-rate birth-death process with uniform sampling.
#            
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readDiscreteCharacterData("data/primates_cytb_small.nex")
#data <- readDiscreteCharacterData("data/test_large.nex")

# Get some useful variables from the data. We need these later on.
taxa <- data.taxa()

n_branches <- taxa.size() * 2 - 3

# set my move index
mvi = 1



######################
# Substitution Model #
######################

#### specify the Jukes-Cantor substitution model applied uniformly to all sites ###
Q <- fnJC(4) 





##############
# Tree model #
##############

# Set up branch length hyperprior with a move
br_len_lambda ~ dnExp(0.2)
moves[mvi++] = mvScale(br_len_lambda, weight=2)

# Define the tree parameter.
# First, we generate the topology.
# We assume a uniform prior on topology.
psi ~ dnUniformTopologyBranchLength(taxa, branchLengthDistribution=dnExponential(br_len_lambda))

# compute the tree length from the phylogeny
tree_length := psi.treeLength()

moves[mvi++] = mvNNI(psi, weight=n_branches/2.0)
moves[mvi++] = mvSPR(psi, weight=n_branches/10.0)
moves[mvi++] = mvBranchLengthScale(psi, weight=n_branches)






###################
# PhyloCTMC Model #
###################

# We use an empirical estimate of the clock rate which is 0.01 (=1%) per million years per site
clockRate <- 1.0

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, branchRates=clockRate, type="DNA")

# attach the data
seq.clamp(data)




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(Q)






monitors[1] = mnModel(filename="output/primates_JC_bl_tree.log",printgen=10, separator = TAB)
monitors[2] = mnFile(filename="output/primates_JC_bl_tree.trees",printgen=10, separator = TAB, psi)

mymcmc = mcmc(mymodel, monitors, moves)


validation = validationAnalysis( mymcmc, 1000 )
validation.burnin(generations=2000,tuningInterval=250)
validation.run(generations=10000)
validation.summarize()


# you may want to quit RevBayes now
q()

