################################################################################
#
# RevBayes Exercise: Data partitioning strategies
# 
# This file: Specifies the full model parameters and moves for a 3-gene
#        partition model with codon specific substitution models
#
# authors: Tracy A. Heath, Michael Landis, Sebastian Hoehna and Brian R Moore
#
################################################################################



#######################
# Reading in the Data #
#######################


# read in the character data
data[1] <- readCharacterData("data/fagus_ITS.nex")
data_rbcL <- readCharacterData("data/fagus_rbcL.nex")

# partition for 1st codon position
data[2] <- data_rbcL
data[2].setCodonPartition(1)

# partition for 2nd codon position
data[3] <- data_rbcL
data[3].setCodonPartition(2)

# partition for 3rd codon position
data[4] <- data_rbcL
data[4].setCodonPartition(3)

data_matK <- readCharacterData("data/fagus_matK.nex")

# partition for 1st codon position
data[5] <- data_matK
data[5].setCodonPartition(1)

# partition for 2nd codon position
data[6] <- data_matK
data[6].setCodonPartition(2)

# partition for 3rd codon position
data[7] <- data_matK
data[7].setCodonPartition(3)

n_parts <- data.size()

# Get the size of each partition
for (i in 1:n_parts)
    n_sites[i] <- data[i].nchar(1)

# Get some useful variables from the data. We need these later on.
n_species <- data[1].ntaxa()
names <- data[1].names()
n_branches <- 2 * n_species - 3



#################################
#      Substitution Model       #
#   Loop over each partition    #
#################################
  
mi <- 0
for (i in 1:n_parts){

  er_prior[i] <- v(1,1,1,1,1,1)
  er[i] ~ dnDirichlet(er_prior[i])
  moves[mi++] <- mvSimplexElementScale(er[i], alpha=10.0, tune=true, weight=3.0)

  pi_prior[i] <- v(1,1,1,1)
  pi[i] ~ dnDirichlet(pi_prior[i])
  moves[mi++] <- mvSimplexElementScale(pi[i], alpha=10.0, tune=true, weight=2.0) 

  Q[i] := gtr(er[i],pi[i])

  alpha_prior_min[i] <- 0.1
  alpha_prior_max[i] <- 50.0
  alpha[i] ~ dnUnif( alpha_prior_min[i], alpha_prior_max[i] )
  norm_gamma_rates[i] := discretizeGamma( alpha[i], alpha[i], 4, false )
  moves[mi++] <- mvScale(alpha[i], lambda=0.8, tune=true, weight=3.0)

  # the probability of a site being invariable
  pinvar[i] ~ beta(1,1)
  # add moves for the probability if a site being invariable
  moves[mi++] <- mvScale(pinvar[i], lambda=0.1, tune=true, weight=2.0)
  moves[mi++] <- mvSlide(pinvar[i], delta=0.1, tune=true, weight=2.0)

  # specify a rate multiplier for each partition
  part_rate_mult[i] ~ dnExponential( 1.0 )

  moves[mi++] <- mvScale(part_rate_mult[i], lambda=1.0, tune=true, weight=2.0)

}

# create a rate multiplier for each partitition that scales the tree
# This doesn't work currently
#part_rate_mult_prior <- rep(n_parts,1)
#part_rate_mult ~ dnDirichlet( part_rate_mult_prior )
#
#moves[mi++] <- mvSimplexElementScale(part_rate_mult, alpha=10, tune=true, weight=2.0)



##############
# Tree model #
##############


# Specify a uniform topology prior
topology ~ uniformTopology(n_species, names)

# moves on the tree
moves[mi++] <- mvNNI(topology, weight=10.0)


#### Specify a prior and moves on the branch lengths #### 
# create a random variable for each branch length using a for loop
for (i in 1:n_branches) {
  # We use here the exponential distribution with rate 1.0 as the branch length prior
  br_lens[i] ~ dnExponential(10.0)
  
  # Add a move for the branch length. We just take a simple scaling move since the value is a positive real number.
  moves[mi++] <- mvScale(br_lens[i],lambda=1,tune=true,weight=1) 
}

# create a deterministic variable for monitoring purposes
TL := sum(br_lens)

# Build the tree by combining the topology with the branch length.
phylogeny := treeAssembly(topology, br_lens)





###################
# PhyloCTMC Model #
###################

for (i in 1:n_parts){
  phyloSeq[i] ~ phyloCTMC(tree=phylogeny, Q=Q[i], branchRates=part_rate_mult[i], siteRates=norm_gamma_rates[i], pInv=pinvar[i], type="DNA")
#  phyloSeq[i] ~ phyloCTMC(tree=phylogeny, Q=Q[i], siteRates=norm_gamma_rates[i], pInv=pinvar[i], type="DNA")
  phyloSeq[i].clamp(data[i])
}




#############
# THE Model #
#############

mymodel <- model(topology)

