#
# Tutorial -- the phylogenetic comparative method
# Correlation analysis: rates, GC content, dates and life-history traits in placental mammals
# Nicolas Lartillot -- July 2014
#

######### 
# data
#########

# read the quantitative trait data 
contData <- readContinuousCharacterData("data/plac40lhtlog.nex")

# get some useful variables from the data
nTraits <- contData.nchar()[1]

# read the sequence data
# the readCharacter function returns a vector of matrices. We just take the first one
seqData <- readDiscreteCharacterData("data/plac40_4fold.nex")

# get some useful variables from the data
nTaxa <- seqData.ntaxa()
names <- seqData.names()
nSites <- seqData.nchar()[1]


######### 
# tree  
#########

# create a birth death process, leading to a tree whose tips match the taxon names given by the dataset

speciation ~ dnLogUniform(0.001,1000)
extinction ~ dnLogUniform(0.001,1000)

# rough estimate of sampling fraction: 40 placental mammals out of the ~4000 currently existing out there
sampling_fraction := 0.01

psi ~ dnBDP(lambda=speciation, mu=extinction, rho=sampling_fraction, rootAge=1.0, nTaxa=nTaxa, names=names)

# work under fixed, externally provided, tree topology
treeArray <- readTrees("data/chronoplac40.tree")
fixedTree <- treeArray[1]
psi.setValue(fixedTree)


######### 
# rate and trait evolution
#########

# covariance matrix of dimension nTraits + 2
# first entry will be log of substitution rate
# then all other entries will stand for the life-history traits

# covariance matrix is inverse Wishart of parameters kappa * Identity and df = nTraits+3 degrees of freedom
# where kappa is itself a scaling factor

kappa ~ dnLogUniform(min=0.001,max=1000)
kappa.setValue(0.1)
df <- nTraits+4
covdim <- nTraits+2

Sigma ~ dnInvWishart(dim=covdim, kappa=kappa, df=df)

# multivariate Brownian process along the tree
X ~ dnBrownianMultiVariate(psi,sigma=Sigma)

# first and second components of the Brownian model map to substitution rate and gc content (see below)
# other copmonents of the Brownian process correspond to life-history traits
# so we clamp the process at the tips of the tree, at the values observed in extant taxa
# you need to do this trait by trait
for (i in 1:nTraits)	{

	# here we say that the (i+2)th entry of the Brownian process should map to the ith. quantitative trait
	X.clampAt(contData,i+2,i)
}

# get useful functions of the covariance matrix
for (i in 1:covdim)	{
	var[i] := Sigma.covariance(i,i)
}

k <- 1;
for (i in 1:(covdim-1))	{
	for (j in (i+1):covdim)	{
		correl[k] := Sigma.correlation(i,j)
		pCorrel[k] := Sigma.partialCorrelation(i,j)
		k <- k+1
	}
}

# get useful outputs from the brownian process

for (i in 1:covdim)	{
	# mean and stdev of the log rate (i=1) and then each trait
	meanX[i] := X.mean(i)
	stdevX[i] := X.stdev(i)
}


######### 
# substitution process
#########

# equilibrium frequencies 
bf <- v(1,1,1,1)
pi ~ dnDirichlet(bf)

# first component of the Brownian model gives the *log* of the substitution rate
branchrates := expbranchtree(tree=psi,process=X,traitindex=1)

# second component of the Brownian model gives the *logit* of the equilibrium GC content
# thus GC = tanh(X_2(t)), where X is the Brownian motion
# tanh is the hyperbolic tangent: tanh(x) = exp(x) / (1 + exp(x))
branchGC := tanhBranchTree(tree=psi,process=X,traitindex=2)

# the transition / transversion ratio
tstv ~ dnLogUniform(0.001,1000)

# each branch has its own substitution matrix, which is a T92 matrix
# defined by a transition/transversion ratio and an equilibrium GC
# so, we use branchGC, which gives a GC for each branch
# we combine it with the global ts/tv ratio
branchMatrices := t92GCBranchTree(tree=psi,branchGC=branchGC,tstv=tstv)


# create the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=branchMatrices, rootFrequencies=pi, branchRates=branchrates, nSites=nSites, type="DNA")

# condition on sequence alignment
seq.clamp(seqData)


# finally, create model
mymodel = model(kappa)


######### 
# moves
#########

# initialize index
index <- 0

# rescaling moves on speciation and extinction rates
moves[++index] = mvScale(speciation, lambda=1, tune=true, weight=3.0)
moves[++index] = mvScale(extinction, lambda=1, tune=true, weight=3.0)

# moves on the tree: only relative dates
moves[++index] = mvSubtreeScale(psi, weight=5.0)
moves[++index] = mvNodeTimeSlideUniform(psi, weight=10.0)

# if we wanted to, we could also move the tree topology
# and integrate over the posterior induced by the sequence data
# but mixing is currently a bit challenging 
# moves[++index] = mvNNI(psi, weight=5.0)
# moves[++index] <- mvFNPR(psi, weight=5.0)

# moves on kappa
moves[++index] = mvScale(kappa, lambda=2.0, tune=true, weight=3.0)

# conjugate Gibbs move on covariance matrix
moves[++index] = mvConjugateInverseWishartBrownian(sigma=Sigma, process=X, kappa=kappa, df=df, weight=1) 

# moves on the Brownian process
moves[++index] = mvMultivariatePhyloProcessSliding(process=X,lambda=10,tune=true,weight=100) 
moves[++index] = mvMultivariatePhyloProcessSliding(process=X,lambda=1,tune=true,weight=100) 
moves[++index] = mvMultivariatePhyloProcessTranslation(process=X,lambda=1,tune=true,weight=1) 
moves[++index] = mvMultivariatePhyloProcessTranslation(process=X,lambda=0.1,tune=true,weight=1) 

# moves on pi
moves[++index] = mvSimplexElementScale(pi, alpha=10.0, tune=true, weight=1.0)

# a move on transition transversion rate
moves[++index] = mvScale(tstv, lambda=2.0, tune=true, weight=3.0)


######### 
# monitors
#########

# screen monitor
monitors[1] = mnScreen(printgen=1000, correl, meanX)

# similar monitor, but directly into file
monitors[2] = mnFile(filename="output/placgc.trace", printgen=10, separator = TAB, correl, var, meanX)

# file monitors for the correlation and partial correlation coefficients
monitors[3] = mnFile(filename="output/placgc.correl",printgen=10, separator = TAB, correl, pCorrel)

# a file monitor just for the covariance matrix
monitors[4] = mnFile(filename="output/placgc.cov",printgen=10, separator = TAB, Sigma)

# a file monitor for the evolution of traits along the tree (in newick format)
monitors[5] = mnFile(filename="output/placgc.traits",printgen=10, separator = TAB, X)

# monitoring the trees and dates
monitors[6] = mnFile(filename="output/placgc.trees",printgen=10, separator = TAB, psi)

# a model monitor
monitors[7] = mnModel(filename="output/placgc.log",printgen=10, separator = TAB)

 
######### 
# mcmc
#########

mymcmc= mcmc(mymodel, monitors, moves)

mymcmc.burnin(generations=2000,tuningInterval=250)

mymcmc.run(5000)


