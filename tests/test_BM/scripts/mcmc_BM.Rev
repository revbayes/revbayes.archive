################################################################################
#
# RevBayes Validation Test: Brownian-Motion on a fixed tree
# 
# Model: Continuous character(s) evolve under Brownian motion.
#            
#
# authors: Sebastian Hoehna
#
################################################################################

seed(12345)

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readContinuousCharacterData("data/primates_lht.nex")

data.excludeAll()
data.includeCharacter(1) 

# Get some useful variables from the data. We need these later on.
taxa <- data.taxa()

# set my move index
mvi = 0



#######################
# BM-Model Parameters #
#######################


# sigma: variance per unit of time of the Brownian motion
sigma ~ dnExponential(1.0)

moves[++mvi] = mvScale(sigma, lambda=1.0, tune=true, weight=2.0) 



##############
# Tree model #
##############


psi <- readTrees( "data/Primates_tree.nex" )[1]




###################
# PhyloCTMC Model #
###################

traits ~ dnPhyloBrownianREML(psi, branchRates=1.0, siteRates=sigma, nSites=1)

# attach the data
traits.clamp(data)




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(psi)


mni = 0
monitors[++mni] = mnModel(filename="output/primates_BM.log",printgen=10, separator = TAB)
monitors[++mni] = mnScreen(printgen=1000, sigma)

mymcmc = mcmc(mymodel, monitors, moves)
mymcmc.burnin(generations=5000,tuningInterval=250)
mymcmc.run(generations=5000)


# you may want to quit RevBayes now
q()

