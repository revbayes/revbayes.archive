################################################################################
#
# RevBayes Validation Test: Brownian-Motion on a fixed tree
# 
# Model: Continuous character(s) evolve under Brownian motion.
#            
#
# authors: Sebastian Hoehna
#
################################################################################

seed(12345)

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readContinuousCharacterData("data/primates_lhtlog.nex")

data.excludeAll()
data.includeCharacter(1) 

# Get some useful variables from the data. We need these later on.
taxa <- data.taxa()

# set my move index
mvi = 0



#######################
# OU-Model Parameters #
#######################


sigma ~ dnLognormal( ln(10.0), sd=0.587405 )
optimum ~ dnUniform(-10,10)
attraction ~ dnExponential(1.0)

moves[++mvi] = mvScale(sigma, lambda=1.0, tune=true, weight=2.0) 
moves[++mvi] = mvSlide(optimum, delta=1.0, tune=true, weight=2.0) 
moves[++mvi] = mvScale(attraction, lambda=1.0, tune=true, weight=2.0) 



##############
# Tree model #
##############


psi <- readTrees( "data/primates.tree" )[1]




###################
# PhyloCTMC Model #
###################

traits ~ dnPhyloOrnsteinUhlenbeckMVN(psi, sigma=sigma, alpha=attraction, theta=optimum, rootState=optimum, nSites=1)

# attach the data
traits.clamp(data)


#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(psi)


mni = 0
monitors[++mni] = mnModel(filename="output/primates_OU.log",printgen=10, separator = TAB)
monitors[++mni] = mnScreen(printgen=100, sigma)

mymcmc = mcmc(mymodel, monitors, moves)
mymcmc.burnin(generations=500,tuningInterval=100)
mymcmc.run(generations=500)


# you may want to quit RevBayes now
q()

