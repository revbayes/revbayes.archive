################################################################################
#
# RevBayes Validation Test: Brownian-Motion on a fixed tree
#
# Model: Continuous character(s) evolve under Brownian motion.
#
#
# authors: Sebastian Hoehna
#
################################################################################

seed(12345)

#######################
# Reading in the Data #
#######################

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

data <- readContinuousCharacterData("data/primates_lhtlog_2.nex")

# Get some useful variables from the data. We need these later on.
taxa <- data.taxa()

# set my move index
mvi = 0

#######################
# BM-Model Parameters #
#######################

# sigma: variance per unit of time of the Brownian motion

rate_log ~ dnUniform(-3,1)
moves[++mvi] = mvSlide(rate_log, weight=2.0)
rate := 10^rate_log

sigma ~ dnLKJ(eta=1, dim=2)
moves[++mvi] = mvCorrelationMatrixSingleElementBeta(sigma, tune=true, weight=2.0)

rates := sigma.diagonal()
covariance := sigma.upperTriangle()

##############
# Tree model #
##############

psi <- readTrees( "data/primates.tree" )[1]




###################
# PhyloCTMC Model #
###################

traits ~ dnPhyloMultivariateBrownianREML(psi, branchRates=rate, rateMatrix=sigma)

# attach the data
traits.clamp(data)

#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(psi)


mni = 0
monitors[++mni] = mnModel(filename="output/primates_mvBM.log",printgen=10, separator = TAB)
monitors[++mni] = mnScreen(printgen=1000, rate, covariance)

mymcmc = mcmc(mymodel, monitors, moves)
mymcmc.burnin(generations=5000,tuningInterval=250)
mymcmc.run(generations=5000)


# you may want to quit RevBayes now
q()
